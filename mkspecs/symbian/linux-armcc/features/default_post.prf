isEmpty(DESTDIR) {
    DESTDIR = .
}

contains(TEMPLATE, lib):!contains(CONFIG, static):!contains(CONFIG, staticlib) {
    exports.commands = winewrapper getexports.exe ${QMAKE_FILE_NAME} > ${OBJECTS_DIR}${QMAKE_FILE_BASE}.exports
    exports.output = $$OBJECTS_DIR/${QMAKE_FILE_BASE}.exports
    exports.input = OBJECTS
    exports.variable_out = ORDINALMAP
    exports.CONFIG = no_link

    deffile.input = ORDINALMAP
    deffile.output = $$OBJECTS_DIR/$${TARGET}.def
    deffile.commands = makedeffile $$OBJECTS_DIR/$${TARGET}.def ${QMAKE_FILE_NAME}
    deffile.variable_out = DEFFILE
    deffile.CONFIG = no_link combine

    expfile.input = DEFFILE
    expfile.output = $$OBJECTS_DIR/$${TARGET}.exp
    expfile.commands = makeexpfile ${QMAKE_FILE_NAME} $$OBJECTS_DIR/$${TARGET}.exp
    expfile.variable_out = QMAKE_LIBS
    expfile.CONFIG = target_predeps

    ordinalmap.input = DEFFILE
    ordinalmap.output = $$DESTDIR/$${TARGET}.lib
    ordinalmap.commands = makeordinalmap $$OBJECTS_DIR ${QMAKE_FILE_NAME} $${TARGET}.dll $$DESTDIR/$${TARGET}.lib
    ordinalmap.variable_out = PRE_TARGETDEPS

    elftran.target = $${DESTDIR}/lib$${TARGET}.tran
    elftran.commands = winewrapper elftran.exe  -version 4.1536 -sid 0xe001b2dc  -allow -nocall -heap 0x00020000 0x00800000 -stack 0x00014000 -unpaged         -uid1 0x10000079 -uid2 0x1000008d -uid3 0xe001b2dc         -fpu softvfp -capability All-Tcb $${DESTDIR}/lib$${TARGET}.dll $${DESTDIR}/lib$${TARGET}.tran
    elftran.depends = $${DESTDIR}/lib$${TARGET}.dll
    QMAKE_POST_LINK += winewrapper elftran.exe  -version 4.1536 -sid 0xe001b2dc  -allow -nocall -heap 0x00020000 0x00800000 -stack 0x00014000 -unpaged         -uid1 0x10000079 -uid2 0x1000008d -uid3 0xe001b2dc         -fpu softvfp -capability All-Tcb $${DESTDIR}/lib$${TARGET}.dll $${DESTDIR}/lib$${TARGET}.tran
    QMAKE_CLEAN += $${DESTDIR}/lib$${TARGET}.tran

    QMAKE_EXTRA_COMPILERS += exports
    QMAKE_EXTRA_COMPILERS += deffile
    QMAKE_EXTRA_COMPILERS += expfile
    QMAKE_EXTRA_COMPILERS += ordinalmap
    QMAKE_EXTRA_TARGETS += elftran

    QMAKE_LIBS += -ledllstub -ledll.lib\\(uc_dll_.o\\)
}

contains(TEMPLATE, app) {
    elftran.target = $${DESTDIR}/$${TARGET}.tran
    elftran.commands = winewrapper elftran.exe  -version 10.0 -sid 0xe4d10fc9  -nocall -heap 0x00020000 0x00800000 -stack 0x00014000 -unpaged         -uid1 0x1000007a -uid2 0x1000008d -uid3 0xe4d10fc9         -fpu softvfp -capability None $${DESTDIR}/$${TARGET} $${DESTDIR}/$${TARGET}.tran
    elftran.depends = $${DESTDIR}/$${TARGET}
    QMAKE_POST_LINK += winewrapper elftran.exe  -version 10.0 -sid 0xe4d10fc9  -nocall -heap 0x00020000 0x00800000 -stack 0x00014000 -unpaged         -uid1 0x1000007a -uid2 0x1000008d -uid3 0xe4d10fc9         -fpu softvfp -capability None $${DESTDIR}/$${TARGET} $${DESTDIR}/$${TARGET}.tran
    QMAKE_CLEAN += $${DESTDIR}/$${TARGET}.tran

    QMAKE_EXTRA_TARGETS += elftran

    QMAKE_LIBS += -leexe.lib\\(uc_exe_.o\\)
}
