exports.commands = winewrapper getexports.exe ${QMAKE_FILE_NAME} > ${OBJECTS_DIR}${QMAKE_FILE_BASE}.exports
exports.output = $$OBJECTS_DIR/${QMAKE_FILE_BASE}.exports
exports.input = OBJECTS
exports.variable_out = ORDINALMAP
exports.CONFIG = no_link

deffile.input = ORDINALMAP
deffile.output = $$OBJECTS_DIR/$${TARGET}.def
deffile.commands = makedeffile $$OBJECTS_DIR/$${TARGET}.def ${QMAKE_FILE_NAME}
deffile.variable_out = DEFFILE
deffile.CONFIG = no_link combine

expfile.input = DEFFILE
expfile.output = $$OBJECTS_DIR/$${TARGET}.exp
expfile.commands = makeexpfile ${QMAKE_FILE_NAME} $$OBJECTS_DIR/$${TARGET}.exp
expfile.variable_out = QMAKE_LIBS
expfile.CONFIG = target_predeps

ordinalmap.input = DEFFILE
ordinalmap.output = $$OBJECTS_DIR/$${TARGET}.lib
ordinalmap.commands = makeordinalmap $$OBJECTS_DIR ${QMAKE_FILE_NAME} $${TARGET}.dll $$OBJECTS_DIR/$${TARGET}.lib
ordinalmap.variable_out = PRE_TARGETDEPS

QMAKE_EXTRA_COMPILERS += exports
QMAKE_EXTRA_COMPILERS += deffile
QMAKE_EXTRA_COMPILERS += expfile
QMAKE_EXTRA_COMPILERS += ordinalmap
