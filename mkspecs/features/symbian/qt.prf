contains(DEFINES, QT_MAKEDLL)|contains(DEFINES, QT_DLL) {
    CONFIG *= epocallowdlldata
}

CONFIG += qtmain

load(qt)

# Add dependency to QtLibs package to all other projects besides QtLibs.
# Note: QtLibs with full capabilities has UID3 of 0x2001E61C, 
#       while self-signed version typically has temporary UID3 of 0xE001E61C. 
contains(CONFIG, qt):!contains(TARGET.UID3, 0x2001E61C):!contains(TARGET.UID3, 0xE001E61C) {
    default_deployment.pkg_prerules += \
        "; Default dependency to Qt libraries" \
        "(0x2001E61C), $${QT_MAJOR_VERSION}, $${QT_MINOR_VERSION}, $${QT_PATCH_VERSION},  {\"QtLibs pre-release\"}"
}

isEmpty(TARGET.EPOCSTACKSIZE):TARGET.EPOCSTACKSIZE = 0x14000
isEmpty(TARGET.EPOCHEAPSIZE):TARGET.EPOCHEAPSIZE = 0x020000 0x800000
